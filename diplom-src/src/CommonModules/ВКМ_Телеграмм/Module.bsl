
#Область ПрограммныйИнтерфейс
//Подключает к сервису "Телеграммм"
//
// Параметры:
// ТекстСообщения - Строка
//     * ТекстСообщения - созданное сообщение при записи документа ВКМ_ОбслуживаниеКлиентов
// Возвращаемое значение:
//  Строка
Функция ПолучениеИОтпрвкаТекстаСообщения(ТекстСообщения) Экспорт
	
	ИдентификаторЧата = Константы.ВКМ_ИндификаторГруппыДляОповещения.Получить(); 
	ТокенБота = Константы.ВКМ_ТокенУправленияТелеграммБотом.Получить();
	
	Если Не ЗначениеЗаполнено(ИдентификаторЧата) Тогда 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не заполненна константа ИндификаторГруппыДляОповещения";
		Сообщение.Сообщить();
		Возврат 400 ;////Код состояния
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТокенБота) Тогда 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не заполненна константа ТокенУправленияТелеграммБотом";
		Сообщение.Сообщить();
		Возврат 400; ////Код состояния
	КонецЕсли;
	
	ДанныеСообщенияВТелеграмм = Новый Структура;
	ДанныеСообщенияВТелеграмм.Вставить("method", "sendMessage");
	ДанныеСообщенияВТелеграмм.Вставить("chat_id", ИдентификаторЧата);
	ДанныеСообщенияВТелеграмм.Вставить("text", ТекстСообщения);
	
	ПараметрыЗаписи = Новый ПараметрыЗаписиJSON(, Символы.Таб);
	Запись = Новый ЗаписьJSON;
	Запись.УстановитьСтроку(ПараметрыЗаписи);
	ЗаписатьJSON(Запись, ДанныеСообщенияВТелеграмм);
	СтрJSON = Запись.Закрыть();
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json; charset=UTF-8");
	
	СоединениеССерверомТелеграмм = Новый HTTPСоединение("api.telegram.org",,,,,, Новый ЗащищенноеСоединениеOpenSSL);
	
	ПараметрыПодключения = "/bot"+ТокенБота+"/sendMessage";
	
	Запрос = Новый HTTPЗапрос(ПараметрыПодключения,Заголовки);
	Запрос.УстановитьТелоИзСтроки(СтрJSON);
	//Ответ = СоединениеССерверомТелеграмм.ОтправитьДляОбработки(Запрос); 
	Ответ = СоединениеССерверомТелеграмм.ВызватьHTTPМетод("POST", Запрос);
	
	Если Ответ.КодСостояния <> 200 Тогда
		ЗаписьЖурналаРегистрации("ОбменССерверомТелеграмм", УровеньЖурналаРегистрации.Ошибка,,,
		ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		СообщениеОВыполнении = "Ошибка в подключении к сервесу Telegram";
	Иначе
		СообщениеОВыполнении = Ответ.ПолучитьТелоКакСтроку();
	КонецЕсли;
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = СообщениеОВыполнении;
	Сообщение.Сообщить();
	
	Возврат Ответ.КодСостояния; 
	
КонецФункции
//Получение  текста сообщения для сервиса "Телеграммм" и удаление отпрвленных из спрвочника
Процедура ВКМ_ОтпрвкаИУдаленеиеЭлементовСправочникаУведомления() Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_УведомлениеТелегаммБоту.Ссылка КАК Ссылка,
	|	ВКМ_УведомлениеТелегаммБоту.ВерсияДанных КАК ВерсияДанных,
	|	ВКМ_УведомлениеТелегаммБоту.Текст КАК Текст
	|ИЗ
	|	Справочник.ВКМ_УведомлениеТелегаммБоту КАК ВКМ_УведомлениеТелегаммБоту";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ОтветТелеграм = ПолучениеИОтпрвкаТекстаСообщения(Выборка.Текст);
		
		Если ОтветТелеграм = 200 Тогда
			ЭлементСпрвочника = Выборка.Ссылка.ПолучитьОбъект();
			ЭлементСпрвочника.Удалить();
		Иначе
			Продолжить;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
