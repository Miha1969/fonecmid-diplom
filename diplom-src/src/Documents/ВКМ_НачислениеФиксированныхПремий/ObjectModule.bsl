#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если СписокСотрудников.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю("Не заполнена табличная часть документа",,"СписокСотрудников","Объект.СписокСотрудников");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	РазмерПремии = Константы.ВКМ_РазмерФиксированнойПермии.Получить();
	Если Не ЗначениеЗаполнено(РазмерПремии) Тогда
		ОбщегоНазначения.СообщитьПользователю("Не установлен размер фиксированой премии. Расчет  не будет выполнен");
		Отказ = Истина;
		Возврат;
	КонецЕсли;      
	
	СформироватьДвижения(РазмерПремии);
	
	РасчитатьДополнительныеНачисленияПремия();

	РасчитатьУдержаниеНДФЛ();

КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура СформироватьДвижения(РазмерПремии)
	
	Для Каждого Строка Из СписокСотрудников Цикл
		
		Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.Премия;
		Движение.ПериодРегистрации = Дата;
		Движение.БазовыйПериодНачало = НачалоМесяца(Дата);
		Движение.БазовыйПериодКонец = КонецМесяца(Дата);//- 86399;
		Движение.Сотрудник = Строка.Сотрудник;
		Движение.Показатель = РазмерПремии;
		
		ДвижениеУдержание = Движения.ВКМ_Удержание.Добавить();
		ДвижениеУдержание.ПериодРегистрации = Дата;
		ДвижениеУдержание.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержание.НДФЛ;
		ДвижениеУдержание.Основание = Движение.ВидРасчета;
		ДвижениеУдержание.БазовыйПериодНачало = НачалоМесяца(Дата);
		ДвижениеУдержание.БазовыйПериодКонец = КонецМесяца(Дата); 
		ДвижениеУдержание.Сотрудник = Строка.Сотрудник; 
		

		Строка.СуммаПремии = РазмерПремии;
		
	КонецЦикла;
	
	Движения.ВКМ_ДополнительныеНачисления.Записать();
	Движения.ВКМ_Удержание.Записать();
	
КонецПроцедуры

Процедура РасчитатьДополнительныеНачисленияПремия()
	
	ПремияРасчитана = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_ДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
	|	ВКМ_ДополнительныеНачисления.БазовыйПериодНачало КАК БазовыйПериодНачало,
	|	ВКМ_ДополнительныеНачисления.БазовыйПериодКонец КАК БазовыйПериодКонец,
	|	ВКМ_ДополнительныеНачисления.Показатель КАК Показатель
	|ИЗ
	|	РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
	|ГДЕ
	|	ВКМ_ДополнительныеНачисления.Регистратор = &Регистратор
	|	И ВКМ_ДополнительныеНачисления.ВидРасчета = &ВидРасчета";
	
	Запрос.УстановитьПараметр("Регистратор",Ссылка);
	Запрос.УстановитьПараметр("ВидРасчета",ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.Премия);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ДополнительныеНачисления[Выборка.НомерСтроки - 1];
		
		Движение.Результат = Движение.Показатель;
		
	КонецЦикла;
	
	Движения.ВКМ_ДополнительныеНачисления.Записать(,Истина);
	
КонецПроцедуры

Процедура РасчитатьУдержаниеНДФЛ() 
	
	РазмерНДФЛ = 13;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВКМ_ДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
	               |	ВКМ_ДополнительныеНачисления.Сотрудник КАК Сотрудник,
	               |	ВКМ_ДополнительныеНачисления.Результат КАК Результат,
	               |	ЕСТЬNULL(ВКМ_УдержаниеБазаВКМ_ДополнительныеНачисления.РезультатБаза, 0) КАК РезультатБаза
	               |ИЗ
	               |	РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_Удержание.БазаВКМ_ДополнительныеНачисления(
	               |				&Измерения,
	               |				&Измерения,
	               |				,
	               |				Регистратор = &Регистратор
	               |					И ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ВКМ_Удержание.НДФЛ)) КАК ВКМ_УдержаниеБазаВКМ_ДополнительныеНачисления
	               |		ПО ВКМ_ДополнительныеНачисления.НомерСтроки = ВКМ_УдержаниеБазаВКМ_ДополнительныеНачисления.НомерСтроки
	               |ГДЕ
	               |	ВКМ_ДополнительныеНачисления.Регистратор = &Регистратор";
	
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник"); 
	//Измерения.Добавить("Подразделение");
	
	Запрос.УстановитьПараметр("Регистратор",Ссылка);
	Запрос.УстановитьПараметр("Измерения",Измерения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_Удержание[Выборка.НомерСтроки - 1];
		
		Движение.Результат = Выборка.Результат * РазмерНДФЛ / 100;
		
		Движение.СуммаОснования = Выборка.Результат;
		
	КонецЦикла;
	
	Движения.ВКМ_Удержание.Записать(,Истина); 

КонецПроцедуры
#КонецОбласти

#КонецЕсли