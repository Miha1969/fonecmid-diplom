#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если СписокСотрудников.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю("Не заполнена табличная часть документа",,"СписокСотрудников","Объект.СписокСотрудников");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	РазмерНДФЛ = Константы.ВКМ_РазмерНДФЛ.Получить(); 
	Если Не ЗначениеЗаполнено(РазмерНДФЛ) Тогда
		ОбщегоНазначения.СообщитьПользователю("Не установлен размер НДФЛ. Расчет удержаний не будет выполнен");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	РазмерПремии = Константы.ВКМ_РазмерФиксированнойПермии.Получить();
	Если Не ЗначениеЗаполнено(РазмерПремии) Тогда
		ОбщегоНазначения.СообщитьПользователю("Не установлен размер фиксированой премии. Расчет  не будет выполнен");
		Отказ = Истина;
		Возврат;
	КонецЕсли;      
	
	СформироватьДвиженияИРасчет(РазмерНДФЛ,РазмерПремии);
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура СформироватьДвиженияИРасчет(РазмерНДФЛ, РазмерПремии)
	
	Для Каждого Строка Из СписокСотрудников Цикл
		
		ДвижениеНДФЛ = Движения.ВКМ_Удержание.Добавить();
		
		Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.Премия;
		Движение.ПериодРегистрации = Дата;
		Движение.БазовыйПериодНачало = НачалоМесяца(Дата);
		Движение.БазовыйПериодКонец = КонецМесяца(Дата)- 86399;
		Движение.Сотрудник = Строка.Сотрудник;
		
		ДвижениеНДФЛ.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержание.НДФЛ;
		ДвижениеНДФЛ.Основание = Движение.ВидРасчета;
		ЗаполнитьЗначенияСвойств(ДвижениеНДФЛ,Движение,,"ВидРасчета");
		
		Движение.Результат = РазмерПремии;
		ДвижениеНДФЛ.Результат = Движение.Результат * РазмерНДФЛ / 100;
				ДвижениеНДФЛ.СуммаОснования = Движение.Результат; 

		Строка.СуммаПремии = РазмерПремии;
		
	КонецЦикла;
	
	Движения.ВКМ_ДополнительныеНачисления.Записать();
	Движения.ВКМ_Удержание.Записать();
	
КонецПроцедуры
#КонецОбласти

#КонецЕсли