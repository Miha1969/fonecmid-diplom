#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)
	//Если табличная часть не заполнена, то нечего записывать в регистры
	Если ВыполненныеРаботы.Количество() > 0 Тогда
		
		Запрос= Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ВКМ_ОбслуживаниеКлиентовВыполненныеРаботы.Ссылка.Клиент КАК Клиент,
		|	ВКМ_ОбслуживаниеКлиентовВыполненныеРаботы.Ссылка.Договор КАК Договор,
		|	СУММА(ВКМ_ОбслуживаниеКлиентовВыполненныеРаботы.ЧасыКОплатеКлиенту) КАК ЧасыКОплатеКлиенту,
		|	ДоговорыКонтрагентов.ВидДоговора КАК ВидДоговора,
		|	МАКСИМУМ(ЕСТЬNULL(ДоговорыКонтрагентов.ВКМ_СтоимостьЧасаРаботы, 0)) КАК СтоимостьЧасаРаботы,
		|	ВКМ_ОбслуживаниеКлиентовВыполненныеРаботы.Ссылка.Специалист КАК Специалист,
		|	МАКСИМУМ(ЕСТЬNULL(ВКМ_УсловияОплатыСотрудниковСрезПоследних.ПроцентОтРабот, ""не Установлен"")) КАК ПроцентОтРабот
		|ИЗ
		|	Документ.ВКМ_ОбслуживаниеКлиентов.ВыполненныеРаботы КАК ВКМ_ОбслуживаниеКлиентовВыполненныеРаботы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|		ПО ВКМ_ОбслуживаниеКлиентовВыполненныеРаботы.Ссылка.Договор = ДоговорыКонтрагентов.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&ДатаДокумента, ) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
		|		ПО ВКМ_ОбслуживаниеКлиентовВыполненныеРаботы.Ссылка.Специалист = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник
		|ГДЕ
		|	ВКМ_ОбслуживаниеКлиентовВыполненныеРаботы.Ссылка = &Ссылка
		|	И ВКМ_ОбслуживаниеКлиентовВыполненныеРаботы.Ссылка.Дата МЕЖДУ ДоговорыКонтрагентов.ВКМ_ДатаНачала И ДоговорыКонтрагентов.ВКМ_ДатаОкончания
		|	И ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбаненскоеОбслуживание)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВКМ_ОбслуживаниеКлиентовВыполненныеРаботы.Ссылка.Клиент,
		|	ВКМ_ОбслуживаниеКлиентовВыполненныеРаботы.Ссылка.Договор,
		|	ДоговорыКонтрагентов.ВидДоговора,
		|	ВКМ_ОбслуживаниеКлиентовВыполненныеРаботы.Ссылка.Специалист";
		
		
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		Запрос.УстановитьПараметр("ДатаДокумента",Дата);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Количество() = 0 Тогда
			Текст = "Данный договор не является абаненским или срок договора истек";
			//@skip-check undefined-variable
			//@skip-check unknown-method-property
			ОбщегоНазначения.СообщитьПользователю(Текст,,"Договор","Объект.Договор");
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
		Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина;
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.ПроцентОтРабот = "не Установлен" Тогда
				Текст = "Специалисту не установлен процент оплаты от работ";
				//@skip-check undefined-variable
				//@skip-check unknown-method-property
				ОбщегоНазначения.СообщитьПользователю(Текст,,"Специалист","Объект.Специалист");
				Отказ = Истина;
				Прервать;
			КонецЕсли;
			
			Если Выборка.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбаненскоеОбслуживание И
				Выборка.СтоимостьЧасаРаботы > 0 Тогда 
				
				Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
				Движение.Период = Дата;
				Движение.Клиент = Клиент;
				Движение.Договор = Договор;
				Движение.КоличествоЧасов = Выборка.ЧасыКОплатеКлиенту;
				Движение.СуммаКОплате = Движение.КоличествоЧасов * Выборка.СтоимостьЧасаРаботы; 
				
				Движение = Движения.ВКМ_ВыполненныеСотрудникомРаботы.ДобавитьПриход();
				Движение.Период = Дата;
				Движение.Сотрудник = Выборка.Специалист;
				Движение.ЧасовКОплате = Выборка.ЧасыКОплатеКлиенту;
				Движение.СуммаКОплате = Движение.ЧасовКОплате * Выборка.СтоимостьЧасаРаботы * Выборка.ПроцентОтРабот / 100;
				
			КонецЕсли; 
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	//Если табл.часть не заполнена, то собираем тескт для отпрвки телегаамм боту
	//Иначе документ полностью оформлен и обслуживние завершено по этому не требуется уведомление
	Если ВыполненныеРаботы.Количество() = 0 Тогда 
		
		ТекстДляОправки = "";
		
		СписокИзменений = Новый Массив;
		
		Если Не ЭтоНовый() Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			|	ВКМ_ОбслуживаниеКлиентов.Номер КАК Номер,
			|	ВКМ_ОбслуживаниеКлиентов.Дата КАК Дата,
			|	ВКМ_ОбслуживаниеКлиентов.ВерсияДанных КАК ВерсияДанных,
			|	ВКМ_ОбслуживаниеКлиентов.ДатаПроведенияРабот КАК ДатаПроведенияРабот,
			|	ВКМ_ОбслуживаниеКлиентов.ВремяНачалаРабот КАК ВремяНачалаРабот,
			|	ВКМ_ОбслуживаниеКлиентов.ОписаниеПроблемы КАК ОписаниеПроблемы,
			|	ВКМ_ОбслуживаниеКлиентов.Специалист КАК Специалист
			|ИЗ
			|	Документ.ВКМ_ОбслуживаниеКлиентов КАК ВКМ_ОбслуживаниеКлиентов
			|ГДЕ
			|	ВКМ_ОбслуживаниеКлиентов.Ссылка = &Ссылка";
			
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			Выборка = Запрос.Выполнить().Выбрать();	
			
			Пока Выборка.Следующий() Цикл
				
				СписокИзменений.Добавить("Для документа " + Выборка.Номер);
				
				Если Выборка.ДатаПроведенияРабот <> ДатаПроведенияРабот Тогда
					СписокИзменений.Добавить("измененна дата проведения работ на " + Формат(ДатаПроведенияРабот,"ДФ=dd.MM.yyyy"));
				КонецЕсли;
				Если Выборка.ВремяНачалаРабот <> ВремяНачалаРабот Тогда
					СписокИзменений.Добавить("измененно время проведения работ на " + Формат(ВремяНачалаРабот,"ДЛФ=T"));
				КонецЕсли;
				Если Выборка.Специалист <> Специалист Тогда
					СписокИзменений.Добавить("измененн исполнитель на " + Специалист);
				КонецЕсли;
				
				Если Выборка.ОписаниеПроблемы <> ОписаниеПроблемы Тогда
					СписокИзменений.Добавить("измененно основание на " + ОписаниеПроблемы);
				КонецЕсли;
				
			КонецЦикла; 
			
			ТекстДляОправки = СтрСоединить(СписокИзменений," ") + " "; 
		Иначе
			ТекстДляОправки = "Создан документ от " + Формат(Дата, "ДФ=dd.MM.yyyy") + ", дата проведение работ: " + 
			Формат(ДатаПроведенияРабот, "ДФ=dd.MM.yyyy") + ", исполнитель: " + Специалист + ", " + ОписаниеПроблемы;
		КонецЕсли; 
		
		Если СписокИзменений.Количество() > 1 Или СписокИзменений.Количество() = 0 Тогда
			НовыйОбъектСпрвочник = Справочники.ВКМ_УведомлениеТелегаммБоту.СоздатьЭлемент();
			НовыйОбъектСпрвочник.Текст = ТекстДляОправки;
			НовыйОбъектСпрвочник.Записать();
		КонецЕсли; 
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
#КонецЕсли